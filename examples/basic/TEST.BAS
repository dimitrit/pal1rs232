10 REM *******************
20 REM *   RS-232-TEST   *
30 REM *******************
40 BA=5864:REM PAL-1 SERIAL I/F ADAPTER BASE ADDRESS
50 PRINT "            RS-232"
60 PRINT "------------------------------"
70 PRINT:PRINT:PRINT
80 PRINT "BAUDRATE: A)  300 Bd [ 600 Bd]"
90 PRINT "          B) 1200 Bd [2400 Bd]"
100 PRINT:PRINT:INPUT "SELEC BAUDRATE ";B$
110 IF B$="A" THEN BD=2:GOTO 130
120 IF B$="B" THEN BD=1 ELSE GOTO 100
130 PRINT:PRINT:PRINT"ENSURE TXD AND RXD ARE SHORTED"
140 PRINT"ALSO SHORT RTS AND CTS"
150 PRINT:PRINT
160 INPUT" <ENTER> ";E$
170 PRINT:PRINT
180 POKE BA,3:REM MASTER RESET
190 POKE BA,16+BD 
200 PRINT "    *** RS-232 TEST ***"
210 IF PEEK(BA) AND 8 THEN PRINT"CTS AND RTS NOT SHORTED":END
220 FOR A=32 TO 127
230 IF PEEK(BA) AND 2 THEN POKE BA+1,A:GOTO 240 ELSE 230
240 IF PEEK(BA) AND 1 THEN GOTO 250 ELSE 240
250 B=PEEK(BA+1)
260 PRINT " ";CHR(A);"-";CHR(B);" ";
270 IF A<>B THEN PRINT"FAILURE: ";PEEK(BA):END
280 NEXT
290 END
