10 REM *******************
20 REM *   RS-232-TEST   *
30 REM *******************
40 BA=5864:REM PAL-1 SERIAL I/F ADAPTER BASE ADDRESS
50 PRINT "            RS-232"
60 PRINT "------------------------------"
70 PRINT:PRINT:PRINT
80 PRINT "BAUDRATE: A)  300 Bd [ 600 Bd]"
90 PRINT "          B) 1200 Bd [2400 Bd]"
100 PRINT:PRINT:INPUT "SELECT BAUDRATE ";B$
110 IF B$="A" THEN BD=2:GOTO 130
120 IF B$="B" THEN BD=1 ELSE GOTO 100
130 PRINT:PRINT:PRINT"ENSURE THE FOLLOWING PINS ARE CONNECTED:"
140 PRINT"    TXD <---> RXD"
150 PRINT"    RTS <---> CTS"
160 PRINT:PRINT
170 INPUT" <ENTER> ";E$
180 PRINT:PRINT
190 POKE BA,3:REM MASTER RESET
200 POKE BA,16+BD:REM SET WORD LENGTH & BAUD RATE 
210 PRINT "    *** RS-232 TEST ***"
220 IF PEEK(BA) AND 8 THEN PRINT"CTS AND RTS NOT SHORTED":END
230 FOR A=32 TO 127
240 IF PEEK(BA) AND 2 THEN POKE BA+1,A ELSE 240
250 IF PEEK(BA) AND 1 THEN GOTO 260 ELSE 250
260 B=PEEK(BA+1)
270 PRINT " ";CHR(A);"-";CHR(B);" ";
280 IF A<>B THEN PRINT"FAILURE: ";PEEK(BA):END
290 NEXT
300 END
